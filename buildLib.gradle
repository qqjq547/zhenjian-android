apply plugin: "com.android.library"
if (Config.depConfig.plugin_kotlin.isApply) {
    apply plugin: "kotlin-android"
}

android {
    compileSdkVersion Config.compileSdkVersion
    defaultConfig {
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode Config.versionCode
        versionName Config.versionName

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    android.libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.name}-${defaultConfig.versionName}-${buildType.name}.aar"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    dataBinding {
        enabled true
    }
}

dependencies {

    if (project.name.endsWith("_pkg") || project.name.endsWith("_mock")) {
        for (def entrySet : ConfigUtils.getApplyExports().entrySet()) {
            api entrySet.value.dep
        }
    } else if (project.name.endsWith("_export")) {
        api Config.depConfig.lib_common.dep
    }
}